---
layout: post
title: A CMake based project template that builds vendor libraries from source
date: 2022-6-22
---

I am practicing writing a renderer in OpenGL following [Learn OpenGL](https://learnopengl.com/).  
They use glfw for windowing + IO and glad as the OpenGL loader. The tutorial focusses more on learning OpenGL then organising your code.  
I'll use this oppurtunity to explain the directory structure I go for while working with CMake + C++. Credits to [ChronicallySerious](https://github.com/ChronicallySerious) for teaching me this structure while working on [Rootex](https://github.com/sdslabs/rootex).

We follow an "out of source" cmake build i.e. all the files generated by cmake lie in a seperate directory that we will call `build`.

The project root looks like this:

```
Mode                 LastWriteTime         Length Name
----                 -------------         ------ ----
d----          20-06-2022    02:13                build
d----          20-06-2022    03:11                src
d----          20-06-2022    02:10                vendor
-a---          20-06-2022    02:10              6 .gitignore
-a---          20-06-2022    02:10             96 .gitmodules
-a---          20-06-2022    02:10            280 CMakeLists.txt
-a---          20-06-2022    02:10             20 README.md
```

All our code lies in `src` while all 3rd party libraries go into `vendor`

`.gitignore` only contains:

```
build/
```

`.gitmodules` will be autogenerated if you are adding any dependency as a [git submodule](https://git-scm.com/book/en/v2/Git-Tools-Submodules).

`CMakeLists.txt`:

```
# Required by CMake
cmake_minimum_required (VERSION 3.5)

# Declare your project name, languages, version
project(
    quadcopter
    LANGUAGES CXX C
    VERSION 1.0
)

# IDEs like VS by default destroy the directory structure for
# projects. So you'll see all .h and .cpp files listed without
# organisation in your IDE. This option organises the code files
# inside the IDE
set_property(GLOBAL PROPERTY USE_FOLDERS ON)
set(CMAKE_CXX_STANDARD 17)

# This is important, We will be using this global variable to
# store all 3rd party library include paths. Normally for an
# OS specific build, there libraries would be present in your
# PATH variable if you've installed them properly. But as we're
# including sources to keep a simple cross platform build, we
# will explicity tell the compiler where all to look when we do
# something like #include <path/to/file.h>
set(QUADCOPTER_INCLUDES CACHE INTERNAL "")

# Make sure vendor is before src as we need to update
# QUADCOPTER_INCLUDES before we actually add definitions for
# our executable
add_subdirectory(vendor)
add_subdirectory(src)
```

### vendor

```
Mode                 LastWriteTime         Length Name
----                 -------------         ------ ----
d----          20-06-2022    02:10                glad
d----          20-06-2022    02:10                glfw
-a---          20-06-2022    02:10             48 CMakeLists.txt
```

`vendor/CMakeLists.txt`:
```
# Include all your dependencies here
add_subdirectory(glfw)
add_subdirectory(glad)
```

All the 3rd party libraries follow the same structure:
```
Mode                 LastWriteTime         Length Name
----                 -------------         ------ ----
d----          23-06-2022    19:12                glfw           # contains the actual library 
-a---          23-06-2022    19:13            418 CMakeLists.txt # a cmake wrapper we'll write around the library
```

Now we generally have 2 cases of 3rd party dependencies:

##### That supply their own CMakeLists.txt : glfw

We can do all the configuration work required in the wrapper `CMakeLists.txt` and then transfer control to the project's CMake configuration.

`vendor/glfw/CMakeLists.txt`:

```
# Append include directory for this project
set(QUADCOPTER_INCLUDES
    ${QUADCOPTER_INCLUDES}
    ${CMAKE_CURRENT_LIST_DIR}/glfw/include
CACHE INTERNAL "")

# we can do any configurations that we need for the project
# here. for example adding `set (BUILD_SHARED_LIBS "ON")`
# will link glfw dynamically instead of statically but that
# is not needed

# Finally transfer control to the dependency's CMake script
add_subdirectory(glfw)
```

##### That don't supply their own CMakeLists.txt : glad

glad is available as a zip which consists of an `include` directory for all the header files and a `src` directory for all the `.c` files.

`vendor/glad/CMakeLists.txt`:

```
# We'll have to manually configure the compilation of this
# library so first we recursively store all the .c and .h
# files in a variable using GLOB_RECURSE
file(GLOB_RECURSE gladC **.c)
file(GLOB_RECURSE gladH **.h)

# We need to tell the language of our library like we told
# for the Quadcopter project. glad is in C.
set_source_files_properties(${gladC} ${gladH} PROPERTIES LANGUAGE C)

# Append the include directory for glad to our global 
# includes variable
set(QUADCOPTER_INCLUDES
    ${QUADCOPTER_INCLUDES}
    ${CMAKE_CURRENT_LIST_DIR}/glad/include
CACHE INTERNAL "")

# Tell CMake to compile these files as a static library. The name
# of the library is specified as the first argument, which will be
# useful while linking
add_library(glad STATIC ${gladC} ${gladH})
# Tell the linker that this is a C project
set_target_properties(glad PROPERTIES LINKER_LANGUAGE C)

# The source files of this library may(glad does) be 
# including its header files as #include <path/to/file.h> 
# so we tell cmake to configure the compiler to include
# library's include path for its compilation
target_include_directories(glad PUBLIC
	glad/include/
)
```

### src

We assume that `src/` contains all your project's `.cpp` and `.h` files and this `CMakeLists.txt` should work for any directory structure within `src/`

`src/CMakeLists.txt`:

```
# Collect all our files
file(GLOB_RECURSE QuadcopterSources **.cpp **.h)
file(GLOB_RECURSE QuadcopterHeaders **.h)

# Compile our files as executable
add_executable(Quadcopter ${QuadcopterSources})

# use the QUADCOPTER_INCLUDES list here to gain access to all
# vendor libraries include paths in your project
target_include_directories(Quadcopter PUBLIC
    ${QUADCOPTER_INCLUDES}
    # This allows you to include your files using <> instead of ""
    ${QuadcopterHeaders}
)

# Link your dependencies
target_link_libraries(Quadcopter PUBLIC
    # We library name for glad ourselves, so use the same name here 
    glad
    # glfw documentation tells that the library is available with this name
    glfw
    # The documentation also says to add this variable which probably 
    # contains dependencies of glfw but I'm not really sure. 
    ${GLFW_LIBRARIES}
)
```

To run this setup with your favourite [generator](https://cmake.org/cmake/help/latest/manual/cmake-generators.7.html) run the following in the root of the project

```
mkdir build/
cd build/
cmake -G "generator name here" ..
```

You can find this setup [here](https://github.com/sin3point14/quadcopter/tree/fe3240c87b2674a4f3eda6cd1c3c4449b1f0431b)
or refer [this](https://github.com/sin3point14/quadcopter/tree/28cd6055afc1fa3ec684d252efdf4f8cfd0ec0b5) to see that it actually works :p
